Put the following in your .emacs to [::MakeMarkVisible] at all times.

<pre>
;;;; Shows mark highlighted with a red background so the user can always see
;;;; where it is. ('mmv' is for 'make mark visible'.)
;;;; By Patrick Gundlach, Teemu Leisti, and Stefan.

(defface mmv-face
  '((t :background "red"))
  "Face used for showing the mark's position.")

(defvar-local mmv-mark-overlay nil "Overlay for showing the mark's position.")

;; The three optional arguments are needed to cover the optional arguments of
;; those functions that this function will modify through `advice-add'.
(defun mmv-show-mark (&rest _)
  "Make the mark's position stand out by means of a one-character-long overlay."
  (unless mmv-mark-overlay
    (setq mmv-mark-overlay (make-overlay 0 0 nil t))
    (overlay-put mmv-mark-overlay 'face 'mmv-face))
  (let ((mark-position (mark t)))
    (cond
     ((null mark-position) (delete-overlay mmv-mark-overlay))
     ((and (< mark-position (point-max))
           (not (eq ?\n (char-after mark-position))))
      (overlay-put mmv-mark-overlay 'after-string nil)
      (move-overlay mmv-mark-overlay
                    mark-position (1+ mark-position)))
     (t
      (overlay-put mmv-mark-overlay 'after-string
                   (propertize " " 'face (overlay-get mmv-mark-overlay 'face)))
      (move-overlay mmv-mark-overlay
                    mark-position mark-position)))))

(add-hook 'pre-redisplay-functions #'mmv-show-mark)
</pre>

[new:DrewAdams:2004-12-25 17:38 UTC]
Interesting alternative to `C-x C-x' (`exchange-point-and-mark') to see the mark.

Might be even more interesting to show ''all'' of the marks (or the N most recent) on the mark ring, not just the (current) mark - perhaps in rainbow colors from recent (red) to old (blue).

And, instead of seeing marks all the time, I'd like to have a binding that shows marks only temporarily - e.g. until some easy event, like a mouse-button release (that is, display marks only while pressed).

BTW, when the mark is at the end of a line, the buffer is highlighted from the line end to the right window edge -- looks like a minor bug. -- DrewAdams

[new]
See also VisibleMark for such sort of things.

[new]
I wrote some elisp that displays the points in the mark ring.  It's modeled after Occur.  The line number for each point in the mark-ring is displayed as well as the contents of that line.  Pressing RET on the line will take you to the point from the mark-ring.  The function is called 'show-marks' and is available at http://therowes.net/~greg/code/emacs/emacs.d/mark.el
